// Common for all projects
allprojects {
  apply plugin: 'java'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.springframework.boot'

  sourceCompatibility = 11
  targetCompatibility = 11

  repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
  }

  ext {
    set('springCloudVersion', "2023.0.0")
  }

  ext {
    set('elasticsearch.version', '6.8.4')
  }

  dependencies {
    // Prod
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'com.vlkan.log4j2:log4j2-logstash-layout:1.0.5'
    implementation 'io.github.cdimascio:java-dotenv:5.2.2'

    implementation 'org.hibernate:hibernate-core:6.2.5.Final'
    implementation 'org.springframework:spring-orm:6.1.3'
    implementation 'org.apache.tomcat:tomcat-dbcp:10.1.18'
    implementation 'com.sun.xml.bind:jaxb-impl:4.0.4'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.springframework:spring-context-support:6.1.3'
    implementation 'org.freemarker:freemarker-gae:2.3.31'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.elasticsearch.client:elasticsearch-rest-client:8.8.1'
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.17.10'
    runtime 'mysql:mysql-connector-java:8.0.33'

    // Test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
  }

  test {
    useJUnitPlatform()

    testLogging {
      events "passed", "skipped", "failed"
    }
  }


  task view_paths {
    doLast { task ->
      println "$task.project.name"
      println "------------------"
      println "Main: $sourceSets.main.java.srcDirTrees"
      println "   Resources: $sourceSets.main.resources.srcDirTrees"
      println "Tests: $sourceSets.test.java.srcDirTrees"
      println "   Resources: $sourceSets.test.resources.srcDirTrees"
    }
  }
}

// All subprojects (located in src/*)
subprojects {
  group = "tv.codely.${rootProject.name}"

  sourceSets {
    main {
      java { srcDirs = ['main'] }
      resources { srcDirs = ['main/resources'] }
    }

    test {
      java { srcDirs = ['test'] }
      resources { srcDirs = ['test/resources'] }
    }
  }

  dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:3.2.2'

    testImplementation rootProject.sourceSets.main.output
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.2'

    if (project.name != "shared") {
      implementation project(":shared")
      testImplementation project(":shared").sourceSets.test.output
    }
  }

  bootJar {
    enabled = false
  }

  jar {
    enabled = true
  }
}

// Main project (located in apps/)
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath('org.springframework.boot:spring-boot-gradle-plugin:3.2.2')
  }
}

sourceSets {
  main {
    java { srcDirs = ['apps/main'] }
    resources { srcDirs = ['apps/main/resources'] }
  }

  test {
    java { srcDirs = ['apps/test'] }
    resources { srcDirs = ['apps/test/resources'] }
  }
}

apply plugin: 'application'

bootJar {
  baseName = 'java-ddd-skeleton'
  version = '0.0.1'
}

mainClassName = 'tv.codely.apps.Starter'

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web:3.2.2'

  implementation project(":shared")
  implementation project(":backoffice")
  implementation project(":mooc")

  testImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.2'
  testImplementation project(":shared").sourceSets.test.output
}

